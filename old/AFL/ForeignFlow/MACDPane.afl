/* 
 * Copyright (C)2017 PT. Stosia Teknologi Investasi
 *
 * This file is licensed under  GNU GPL (General Public License) version 3.
 *
 * File ini dilisensikan kepada Anda, bukan  Anda miliki.  Pemilik file ini
 * adalah pemegang  hak cipta ("copyright holder") di atas. Gunakanlah file
 * ini sesuai dengan lisensi yang telah ditetapkan untuk file ini.
 *
 * File ini  berlisensi  GNU GPL (General Public License)  versi 3. Artinya
 * kalau Anda  memodifikasi file ini, atau membuat karya lain yang berbasis
 * file ini  ("derivative work"),  maka Anda  wajib  mendistribusikan  kode
 * modifikasi atau karya tersebut kepada publik. Hal ini untuk menjaga agar
 * pengetahuan yang ada di sini menjadi lebih berkembang  di masa mendatang 
 * untuk kemajuan kita bersama.
 *
 * Silakan memakai kode ini untuk kepentingan apapun termasuk untuk mencari
 * profit di pasar saham baik secara perorangan atau organisasi, asal tidak
 * jahat. Sebagai tambahan permintaan, kami menghimbau untuk TIDAK  menjual
 * kode/file  ini secara  apa adanya  atau dalam bundel  produk yang dijual
 * secara komersial, karena hal itu tidak etis.
 *
 * Untuk penjelasan lebih lanjut silakan bertanya kepada kami.  Untuk detil
 * lisensi GPLv3  silakan lihat file GPLv3-LICENSE.md  yang didistribusikan
 * bersama file ini.
 *
 * Author: Benny Prijono <benny@stosia.com>
 * Contributors:
 *  - 
 */
_t0 = GetPerformanceCounter();

#include_once <..\Quant.id\AFL\Common.afl>
#include_once <..\Quant.id\AFL\ForeignFlow\ForeignFlowCommon.afl>


_SECTION_BEGIN("Foreign MACD");

	maPeriod1 = Param("EMA 1", 12, 1, 200, 1);
	maPeriod2 = Param("EMA 2", 26, 1, 200, 1);
	maPeriod3 = Param("EMA 3", 9, 1, 200, 1);

	stSetBackgroundTitle(StrFormat(Name() + " Foreign MACD(%g, %g, %g)", maPeriod1, maPeriod2, maPeriod3));
	stSetBackgroundSubtitle(QID_VERSION_LINE);
	
	ffHolding = stGetFFHolding() / 1000000.0 / LOT;
	
	// For intraday chart, only show MACD for today
	if (Interval() < inDaily) {
		Dn = DateNum();
		Last_DN = Dn[BarCount-1];
										 
		ffHolding = IIf(Dn == Last_DN, ffHolding, 0);
	}
	
	EMA_L = EMA(ffHolding, maPeriod2);
	EMA_S = EMA(ffHolding, maPeriod1);
	
	F_MACD = EMA_S - EMA_L;
	Sig = EMA(F_MACD, maPeriod3);
	
	Plot(stAutoPrec(F_MACD), "MACD (Jt Lot)", ColorBlend( colorBlue, GetChartBkColor(), 0.5  ), styleLine | styleThick);
	Plot(stAutoPrec(Sig), "Signal", ColorBlend( colorBlue, GetChartBkColor(), 0.5  ), styleDashed);
	
	Hist = F_MACD - Sig;
	AHist = abs(Hist);
	Plot(stAutoPrec(AHist), "Hist", IIf(Hist >= 0, colorLightBlue, colorLightOrange), styleHistogram | styleOwnScale, 0, HighestVisibleValue(AHist) * 2, 0, 0, 4);
	
	Buy = Cross(F_MACD, Sig);
	Sell = Cross(Sig, F_MACD);
	PlotShapes( Buy * shapeUpArrow, colorGreen, 0, Sig); 
	PlotShapes( Sell * shapeDownArrow, colorRed, 0, F_MACD);
	
_SECTION_END();

_t1 = GetPerformanceCounter();
_TRACE(Name() + " Foreign MACD: " + Int(_t1 - _t0) + " ms");
